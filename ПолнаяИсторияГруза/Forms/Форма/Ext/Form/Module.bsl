#Область ВнутренняяЛогика

&НаСервере 
Процедура ЗаполнитьДеревоПоОбъекту(Объект)
	ДеревоИстории = ДанныеФормыВЗначение(дзПолнаяИсторияГруза, Тип("ДеревоЗначений"));
	ДеревоИстории.Строки.Очистить();
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.СерииНоменклатуры") Тогда
		
		ЗаполнитьДеревоПоСерии(Объект,ДеревоИстории);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.ВМТП_КоносаментВходящий") Тогда
		
		ЗаполнитьДеревоПоКоносаменту(ДеревоИстории, Объект,0);

		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.ВМТП_ФормированиеРасформирование") Тогда
		
		ЗаполнитьПоРасформированию(ДеревоИстории, Объект,0);

		
	КонецЕсли;
	ЗначениеВДанныеФормы(ДеревоИстории, дзПолнаяИсторияГруза);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоРасформированию(Знач ДеревоИстории, Знач Объект, СчетчикРекурсии)
	
	Перем СтрокаГруз, Узел;
	
	Узел = ДеревоИстории.Строки.Добавить(); 	
	Узел.Объект = Объект; 
	Узел.ПометкаУдаления = Объект.ПометкаУдаления;
	Для Каждого СтрокаГруз Из Объект.ГрузыПриемники Цикл
		ЗаполнитьДеревоИсторииПоГрузу(СтрокаГруз.Серия, Узел, СтрокаГруз.ОписаниеГруза, СчетчикРекурсии); 
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПоКоносаменту(Знач ДеревоИстории, Знач Объект, СчетчикРекурсии)
	
	Перем СтрокаГруз, Узел;
	
	Узел = ДеревоИстории.Строки.Добавить(); 	
	Узел.Объект = Объект; 
	Узел.ПометкаУдаления = Объект.ПометкаУдаления;
	Для Каждого СтрокаГруз Из Объект.Грузы Цикл
		ЗаполнитьДеревоИсторииПоГрузу(СтрокаГруз.Груз, Узел, СтрокаГруз.ОписаниеГруза, СчетчикРекурсии); 
	КонецЦикла;

КонецПроцедуры


&НаСервере 
Процедура ЗаполнитьДеревоПоСерии(Груз,ДеревоИстории)
	
	ОписаниеГруза = ПолучитьОписаниеГруза(Груз);
	
	СчетчикРекурсии = 0;
	
	ЗаполнитьДеревоИсторииПоГрузу(Груз, ДеревоИстории, ОписаниеГруза, СчетчикРекурсии);
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьДеревоИсторииПоГрузу(Груз, РодительскийУзел, ПервичноеОписание, СчетчикРекурсии)
	
	СчетчикРекурсии = СчетчикРекурсии + 1;
	
	Если СчетчикРекурсии > 1000 Тогда
		Возврат;
	КонецЕсли;
	
	Узел = РодительскийУзел.Строки.Добавить(); 	
	Узел.Объект = Груз;
	Статус           = ВМТП_ГрузообработкаСервер.ПолучитьСтатусГруза(Груз);
	ИДВнешнейСистемы = ВМТП_ГрузообработкаСервер.ПолучитьИдентификаторГруза(Груз);
	
	Узел.КодГруза = Груз.Код+" / "+ИДВнешнейСистемы+" / "+Статус;
	
	ПараметрыГруза = ПолучитьПараметрыГруза(Груз, ПервичноеОписание);
	
	ЗаполнитьЗначенияСвойств(Узел, ПараметрыГруза);
	
	ДокументыГруза = НайтиВсеДокументыГруза(Груз);
	
	Для Каждого Документ Из ДокументыГруза Цикл
		ДокументУзел = Узел.Строки.Добавить();		
		ДокументУзел.Объект = Документ;
		ДокументУзел.ПометкаУдаления = Документ.ПометкаУдаления или не Документ.Проведен;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ВМТП_ФормированиеРасформирование") Тогда
			ЗаполнитьДеревоПоРасформированию(ПервичноеОписание, Документ, СчетчикРекурсии, ДокументУзел);
		КонецЕсли;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ВМТП_СтрокаТальманскойРасписки") Тогда
			ДокументУзел.КоличествоМест = Документ.Количество;
			ЗаполнитьТаможенныеДвиженияПоОписанию(Документ, ДокументУзел, ПервичноеОписание);
		КонецЕсли;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ВМТП_ДО1") Тогда
			ЗаполнитьСведенияПоДокументуДО(Груз, Документ, ДокументУзел);
		КонецЕсли;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ВМТП_ДО2") Тогда
			ЗаполнитьСведенияПоДокументуДО(Груз, Документ, ДокументУзел);
		КонецЕсли;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ВМТП_КоммерческийАкт") Тогда
			ЗаполнитьСведенияПоДокументуДО(Груз, Документ, ДокументУзел);
		КонецЕсли;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ВМТП_УстановкаОписанияГруза") Тогда
			
			ДокументУзел.Вес = Документ.Вес;
			ДокументУзел.КоличествоМест = Документ.КоличествоМест;
			ЗаполнитьТаможенныеДвиженияПоОписанию(Документ, ДокументУзел, ПервичноеОписание);
		КонецЕсли;
		
		//ГрузыРасформирования = ПолучитьГрузыОписанияГрузов(Расформирование, ПервичноеОписание);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
&НаСервере
Процедура ЗаполнитьСведенияПоДокументуДО(Груз, Знач Документ, Знач ДокументУзел)
	
	Перем Статус, СтрокаДо;
	
	СтрокаДо = Документ.Грузы.Найти(Груз, "Груз");
	Статус = СтрокаДо.ПодробноеОписание +" / " +  Документ.Статус;
	ДокументУзел.КодГруза = Статус;
	ДокументУзел.Вес = СтрокаДо.Вес;
	ДокументУзел.КоличествоМест = СтрокаДо.Количество;
	
	
	
	ЗаполнитьТаможенныеДвиженияДО(Документ, ДокументУзел, СтрокаДо);
	
	
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаможенныеДвиженияДО(Знач Документ, Знач ДокументУзел, Знач СтрокаДо)
	
	Перем Выборка, Запрос, РезультатЗапроса;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВМТП_СведенияДО.ВесКПодачеДО1 КАК ВесКПодачеДО1,
	|	ВМТП_СведенияДО.ВесКПодачеДО2 КАК ВесКПодачеДО2,
	|	ВМТП_СведенияДО.КоличествоМестКПодачеДО1 КАК КоличествоМестКПодачеДО1,
	|	ВМТП_СведенияДО.КоличествоМестКПодачеДО2 КАК КоличествоМестКПодачеДО2,
	|	ВМТП_СведенияДО.КоличествоМестПоданоДО1 КАК КоличествоМестПоданоДО1,
	|	ВМТП_СведенияДО.ВесПоданоДО1 КАК ВесПоданоДО1,
	|	ВМТП_СведенияДО.КоличествоМестПоданоДО2 КАК КоличествоМестПоданоДО2,
	|	ВМТП_СведенияДО.ВесПоданоДО2 КАК ВесПоданоДО2,
	|	ВМТП_СведенияДО.ДокументПоступления КАК ДокументПоступления,
	|	ВМТП_СведенияДО.ОписаниеГруза КАК ОписаниеГруза
	|ПОМЕСТИТЬ ДвижениеДокумента
	|ИЗ
	|	РегистрНакопления.ВМТП_СведенияДО КАК ВМТП_СведенияДО
	|ГДЕ
	|	ВМТП_СведенияДО.Регистратор = &Регистратор
	|	И ВМТП_СведенияДО.ОписаниеГруза = &ОписаниеГруза
	|	И ВМТП_СведенияДО.НомерСтроки = &НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВМТП_СведенияДООстатки.КоличествоМестКПодачеДО1Остаток КАК КоличествоМестКПодачеДО1Остаток,
	|	ВМТП_СведенияДООстатки.ВесКПодачеДО1Остаток КАК ВесКПодачеДО1Остаток,
	|	ВМТП_СведенияДООстатки.КоличествоМестКПодачеДО2Остаток КАК КоличествоМестКПодачеДО2Остаток,
	|	ВМТП_СведенияДООстатки.ВесКПодачеДО2Остаток КАК ВесКПодачеДО2Остаток,
	|	ДвижениеДокумента.ВесКПодачеДО1 + ДвижениеДокумента.ВесКПодачеДО2 КАК ВесКПодачеДО1,
	|	ДвижениеДокумента.КоличествоМестКПодачеДО1 + ДвижениеДокумента.КоличествоМестКПодачеДО2 КАК КоличествоМестКПодачеДО1
	|ИЗ
	|	РегистрНакопления.ВМТП_СведенияДО.Остатки(&Период, ОписаниеГруза = &ОписаниеГруза) КАК ВМТП_СведенияДООстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвижениеДокумента КАК ДвижениеДокумента
	|		ПО ВМТП_СведенияДООстатки.ДокументПоступления = ДвижениеДокумента.ДокументПоступления
	|			И ВМТП_СведенияДООстатки.ОписаниеГруза = ДвижениеДокумента.ОписаниеГруза";
	
	Запрос.УстановитьПараметр("НомерСтроки", СтрокаДо.НомерСтроки);
	Запрос.УстановитьПараметр("ОписаниеГруза", СтрокаДо.ОписаниеГруза);
	Запрос.УстановитьПараметр("Регистратор", Документ);
	Запрос.УстановитьПараметр("Период", Документ.Дата);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ДокументУзел.ТаможняОстатки =  Строка(Выборка.КоличествоМестКПодачеДО1Остаток) +"/"+ Строка(Выборка.ВесКПодачеДО1Остаток);
		ДокументУзел.ТаможняОборот = Строка(Выборка.КоличествоМестКПодачеДО1) + "/" + Строка(Выборка.ВесКПодачеДО1);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаможенныеДвиженияПоОписанию(Знач Документ, Знач ДокументУзел, ОписаниеГруза)
	
	Перем Выборка, Запрос, РезультатЗапроса;
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВМТП_СведенияДО.ВесКПодачеДО1 КАК ВесКПодачеДО1,
	|	ВМТП_СведенияДО.ВесКПодачеДО2 КАК ВесКПодачеДО2,
	|	ВМТП_СведенияДО.КоличествоМестКПодачеДО1 КАК КоличествоМестКПодачеДО1,
	|	ВМТП_СведенияДО.КоличествоМестКПодачеДО2 КАК КоличествоМестКПодачеДО2,
	|	ВМТП_СведенияДО.КоличествоМестПоданоДО1 КАК КоличествоМестПоданоДО1,
	|	ВМТП_СведенияДО.ВесПоданоДО1 КАК ВесПоданоДО1,
	|	ВМТП_СведенияДО.КоличествоМестПоданоДО2 КАК КоличествоМестПоданоДО2,
	|	ВМТП_СведенияДО.ВесПоданоДО2 КАК ВесПоданоДО2,
	|	ВМТП_СведенияДО.ДокументПоступления КАК ДокументПоступления,
	|	ВМТП_СведенияДО.ОписаниеГруза КАК ОписаниеГруза
	|ПОМЕСТИТЬ ДвижениеДокумента
	|ИЗ
	|	РегистрНакопления.ВМТП_СведенияДО КАК ВМТП_СведенияДО
	|ГДЕ
	|	ВМТП_СведенияДО.Регистратор = &Регистратор
	|	И ВМТП_СведенияДО.ОписаниеГруза = &ОписаниеГруза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВМТП_СведенияДООстатки.КоличествоМестКПодачеДО1Остаток КАК КоличествоМестКПодачеДО1Остаток,
	|	ВМТП_СведенияДООстатки.ВесКПодачеДО1Остаток КАК ВесКПодачеДО1Остаток,
	|	ВМТП_СведенияДООстатки.КоличествоМестКПодачеДО2Остаток КАК КоличествоМестКПодачеДО2Остаток,
	|	ВМТП_СведенияДООстатки.ВесКПодачеДО2Остаток КАК ВесКПодачеДО2Остаток,
	|	ДвижениеДокумента.ВесКПодачеДО1 + ДвижениеДокумента.ВесКПодачеДО2 КАК ВесКПодачеДО1,
	|	ДвижениеДокумента.КоличествоМестКПодачеДО1 + ДвижениеДокумента.КоличествоМестКПодачеДО2 КАК КоличествоМестКПодачеДО1
	|ИЗ
	|	РегистрНакопления.ВМТП_СведенияДО.Остатки(&Период, ОписаниеГруза = &ОписаниеГруза) КАК ВМТП_СведенияДООстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвижениеДокумента КАК ДвижениеДокумента
	|		ПО ВМТП_СведенияДООстатки.ДокументПоступления = ДвижениеДокумента.ДокументПоступления
	|			И ВМТП_СведенияДООстатки.ОписаниеГруза = ДвижениеДокумента.ОписаниеГруза";
	
	Запрос.УстановитьПараметр("ОписаниеГруза", ОписаниеГруза);
	Запрос.УстановитьПараметр("Регистратор", Документ);
	Запрос.УстановитьПараметр("Период", Документ.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДокументУзел.ТаможняОстатки =  Строка(Выборка.КоличествоМестКПодачеДО1Остаток) +"/"+ Строка(Выборка.ВесКПодачеДО1Остаток);
		ДокументУзел.ТаможняОборот = Строка(Выборка.КоличествоМестКПодачеДО1) + "/" + Строка(Выборка.ВесКПодачеДО1);
	КонецЕсли;
	
КонецПроцедуры



&НаСервере
Процедура ЗаполнитьДеревоПоРасформированию(Знач ПервичноеОписание, Знач Расформирование, СчетчикРекурсии, Знач Узел)
	
	
	Для Каждого СтрокаГруз Из Расформирование.ГрузыПриемники Цикл
		
		Если ПервичноеОписание <> СтрокаГруз.ОписаниеГруза Тогда
			Продолжить;				
		КонецЕсли;
		
		ЗаполнитьДеревоИсторииПоГрузу(СтрокаГруз.Серия, Узел, ПервичноеОписание, СчетчикРекурсии); 			
		
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Функция НайтиВсеДокументыГруза(Груз)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВМТП_ФормированиеРасформированиеГрузыИсточники.Ссылка КАК Ссылка,
	|	ВМТП_ФормированиеРасформированиеГрузыИсточники.Ссылка.Дата КАК Дата
	|ИЗ
	|	Документ.ВМТП_ФормированиеРасформирование.ГрузыИсточники КАК ВМТП_ФормированиеРасформированиеГрузыИсточники
	|ГДЕ
	|	ВМТП_ФормированиеРасформированиеГрузыИсточники.Серия = &Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ  РАЗЛИЧНЫЕ
	|	ВМТП_УстановкаОписанияГруза.Ссылка,
	|	ВМТП_УстановкаОписанияГруза.Дата
	|ИЗ
	|	Документ.ВМТП_УстановкаОписанияГруза КАК ВМТП_УстановкаОписанияГруза
	|ГДЕ
	|	ВМТП_УстановкаОписанияГруза.Серия = &Серия
	|
	|СГРУППИРОВАТЬ ПО
	|	ВМТП_УстановкаОписанияГруза.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВМТП_СтрокаТальманскойРасписки.Ссылка,
	|	ВМТП_СтрокаТальманскойРасписки.Дата
	|ИЗ
	|	Документ.ВМТП_СтрокаТальманскойРасписки КАК ВМТП_СтрокаТальманскойРасписки
	|ГДЕ
	|	ВМТП_СтрокаТальманскойРасписки.Серия = &Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВМТП_КоммерческийАктГрузы.Ссылка,
	|	ВМТП_КоммерческийАктГрузы.Ссылка.Дата
	|ИЗ
	|	Документ.ВМТП_КоммерческийАкт.Грузы КАК ВМТП_КоммерческийАктГрузы
	|ГДЕ
	|	ВМТП_КоммерческийАктГрузы.Груз = &Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВМТП_ДО1Грузы.Ссылка,
	|	ВМТП_ДО1Грузы.Ссылка.Дата
	|ИЗ
	|	Документ.ВМТП_ДО1.Грузы КАК ВМТП_ДО1Грузы
	|ГДЕ
	|	ВМТП_ДО1Грузы.Груз = &Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВМТП_ДО2Грузы.Ссылка,
	|	ВМТП_ДО2Грузы.Ссылка.Дата
	|ИЗ
	|	Документ.ВМТП_ДО2.Грузы КАК ВМТП_ДО2Грузы
	|ГДЕ
	|	ВМТП_ДО2Грузы.Груз = &Серия
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	Запрос.УстановитьПараметр("Серия", Груз);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");	
	
КонецФункции


&НаСервере
Функция ПолучитьОписаниеГруза(Груз)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВМТП_ИсторияОписанияГрузовСрезПоследних.ОписаниеГрузов КАК ОписаниеГруза
	|ИЗ
	|	РегистрСведений.ВМТП_ИсторияОписанияГрузов.СрезПоследних(, Груз = &Груз) КАК ВМТП_ИсторияОписанияГрузовСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ВМТП_ИсторияОписанияГрузовСрезПоследних.ОписаниеГрузов";
	Запрос.УстановитьПараметр("Груз", Груз);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОписаниеГруза;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции


&НаСервере
Функция ПолучитьПараметрыГруза(Груз, ОписаниеГруза)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВМТП_ИсторияОписанияГрузовСрезПоследних.ПараметрОписания КАК ПараметрОписания,
	|	ВМТП_ИсторияОписанияГрузовСрезПоследних.ЗначениеПараметра КАК ЗначениеПараметра
	|ИЗ
	|	РегистрСведений.ВМТП_ИсторияОписанияГрузов.СрезПоследних(
	|			,
	|			Груз = &Груз
	|				И ОписаниеГрузов = &ОписаниеГрузов
	|				И (ПараметрОписания = ЗНАЧЕНИЕ(Перечисление.ВМТП_ПараметрыОписанияГрузов.КоличествоМест)
	|					ИЛИ ПараметрОписания = ЗНАЧЕНИЕ(Перечисление.ВМТП_ПараметрыОписанияГрузов.Вес))) КАК ВМТП_ИсторияОписанияГрузовСрезПоследних";
	Запрос.УстановитьПараметр("Груз", Груз);
	Запрос.УстановитьПараметр("ОписаниеГрузов", ОписаниеГруза);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПараметрыГруза = Новый Структура;
	
	Пока Выборка.Следующий() Цикл
		
		Параметр = "";
		Если Выборка.ПараметрОписания = Перечисления.ВМТП_ПараметрыОписанияГрузов.КоличествоМест Тогда
			Параметр = "КоличествоМест";
		Иначе
			Параметр = "Вес";
		КонецЕсли;
		
		ПараметрыГруза.Вставить(Параметр, Выборка.ЗначениеПараметра);
		
	КонецЦикла;
	
	Возврат ПараметрыГруза;	 
	
КонецФункции


#КонецОбласти


#Область ОбработчикиСобытийФормы


&НаКлиенте
Процедура Сформировать(Команда)
	ЗаполнитьДеревоПоОбъекту(ОбъектПостроенияИстории);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектПостроенияИсторииПриИзменении(Элемент)
	Сформировать(Неопределено);
КонецПроцедуры


#КонецОбласти